//------------------------------------------------------------------------------
// [2 Call-site Sensitive + Heap Analysis]
//
// In this analysis, context is the cons of the latest 2 invocation
// instructions, whereas the allocation context consists of just the
// latest invocation instruction.
//------------------------------------------------------------------------------

.type ContextItem = symbol | Instruction
.type Context = [?item0: ContextItem, ?item1: ContextItem]
.type AllocationContext = [?ci: ContextItem]

.decl context(?ctx:Context)
.decl alloc_context(?allocCtx:AllocationContext)
.decl context_item(?item:ContextItem)

//-----------------------------------------------------
// Context Constructors
//-----------------------------------------------------

.decl alloc_context_new(?item: ContextItem, ?aCtx:AllocationContext)

.decl context_request(?callerCtx: Context, ?aCtx: AllocationContext, ?invoc: Instruction, ?tofunction: FunctionDecl)
.decl context_response(?callerCtx: Context, ?aCtx: AllocationContext, ?invoc: Instruction, ?ctx: Context)

context(?ctx),
context_item(?invoc),
context_response(?callerCtx, ?aCtx, ?invoc, ?ctx) :-
  context_request(?callerCtx, ?aCtx, ?invoc, _),
  context_item(?item0),
  context_item(?newItem0),
  ?callerCtx = [?item0, ?newItem0],
  ?ctx = [?newItem0, ?invoc].

.decl alloc_context_request(?invoc: ContextItem)
.decl alloc_context_response(?invoc: ContextItem, ?ctx: AllocationContext)

alloc_context(?aCtx),
alloc_context_new(?invoc, ?aCtx),
alloc_context_response(?invoc, ?aCtx) :-
  alloc_context_request(?invoc),
  ?aCtx = [?invoc].


//-----------------------------------------------------
// Context Item Constructors
//-----------------------------------------------------

.decl context_item_by_invoc(?invoc:Instruction, ?ctxItem:ContextItem)
.decl context_item_invoc(?ctxItem:ContextItem, ?invoc:Instruction)

context_item_invoc(?ctxItem, ?invoc) :-
  context_item_by_invoc(?invoc, ?ctxItem).

context_item(?ctxItem),
context_item_by_invoc(?instr, ?ctxItem) :-
  (call_instruction(?instr);
   invoke_instruction(?instr)),
  ?ctxItem = ?instr.


// Dummy context items (needed for empty contexts)
.decl matches_string_context(?str:symbol)

matches_string_context(?str):-
  ?str = "<<empty-context>>";
  ?str = "<<main-context>>";
  ?str = "<<startup-context>>";
  ?str = "<<exit-context>>".

.decl dummy_context_item_by_str(?str:symbol,?ctxItem:ContextItem)
context_item(as(?ctxItem, ContextItem)),
dummy_context_item_by_str(?str, ?ctxItem) :-
  matches_string_context(?str),
  ?ctxItem = ?str.

//-----------------------------------------------------
// Macro-constructors
//-----------------------------------------------------

// record[Ctx, Alloc] = ACtx ->
//    context(Ctx), allocation(Alloc), alloc_context(ACtx).

// merge[CallerCtx, Invoc, ACtx, Alloc] = CalleeCtx ->
//    context(CallerCtx), context(CalleeCtx),
//    instruction(Invoc), alloc_context(ACtx), allocation(Alloc).

// // Macro-definitions
// alloc_context:new[Item] = ACtx <-
//    record[Ctx, _] = ACtx,
//    context:new[_, Item] = Ctx.

// context:new[LastItem, NewItem] = CalleeCtx <-
//    merge[CallerCtx, Invoc, _, _] = CalleeCtx,
//    context:new[_, LastItem] = CallerCtx,
//    context_item:by_invoc[Invoc] = NewItem.

//-----------------------------------------------------
// Unpacking context items
//-----------------------------------------------------

.decl context_getitem0(?ctx:Context, ?item:ContextItem)
context_getitem0(?ctx, ?item):-
  context_item(?item),
  context_response(?a,?b,?c,[_,?item]),
  context_response(?a,?b,?c,?ctx).

.decl context_getitem1(?ctx:Context, ?item:ContextItem)
context_getitem1(?ctx, ?item):-
  context_item(?item),
  context_response(?a,?b,?c,[?item,_]),
  context_response(?a,?b,?c,?ctx).

.decl context_getinvoc0(?ctx:Context, ?invoc:Instruction)
context_getinvoc0(?ctx, ?invoc):-
  context_getitem0(?ctx, ?item),
  context_item_invoc(?item, ?invoc).

.decl context_getinvoc1(?ctx:Context, ?invoc:Instruction)
context_getinvoc1(?ctx, ?invoc):-
  context_getitem1(?ctx, ?item),
  context_item_invoc(?item, ?invoc).

.decl alloc_context_getitem(?aCtx:AllocationContext, ?item:ContextItem)
alloc_context_getitem(?aCtx, ?item):-
  alloc_context_new(?item, ?aCtx).

.decl alloc_context_getinvoc(?aCtx:AllocationContext, ?invoc:Instruction)
alloc_context_getinvoc(?aCtx, ?invoc) :-
  alloc_context_getitem(?aCtx, ?item),
  context_item_invoc(?item, ?invoc).

//Already generated output relations of 1-call-sensitivity
.decl context_getinvoc(?dummy : number)
.decl context_getitem(?ctx: Context, ?ctxItem: ContextItem)

context_getitem(?ctx, ?ctxItem) :-
  context_response(_, _, ?ctxItem, ?ctx).
