//------------------------------------------------------------------------------
// [Types]
//------------------------------------------------------------------------------

.type Type
.decl type(t:Type)


// Sizes of types in bytes

.decl type_has_size(t:Type, size:number)
.decl type_has_unpadded_size(t:Type, size:number)

// For constraints
.decl schema_invalid_type(type:Type, file:File, line:Line)


//------------------------------------------------------------------------------
// [Primitive Types]
//------------------------------------------------------------------------------

.type PrimitiveType = Type
.type IntegerType = PrimitiveType
.type FPType = PrimitiveType
.type VoidType = PrimitiveType
.type LabelType = PrimitiveType
.type MetadataType = PrimitiveType

.decl primitive_type(type:PrimitiveType)
.decl integer_type(type:IntegerType)
.decl fp_type(type:FPType)
.decl void_type(type:VoidType)
.decl label_type(type:LabelType)
.decl metadata_type(type:MetadataType)
.decl x86mmx_type(type:PrimitiveType)

type(t) :- primitive_type(t).
primitive_type(t) :- integer_type(t).
primitive_type(t) :- fp_type(t).
primitive_type(t) :- void_type(t).
primitive_type(t) :- label_type(t).
primitive_type(t) :- metadata_type(t).
primitive_type(t) :- x86mmx_type(t).


// Standard types as constants

void_type("void").
label_type("label").
metadata_type("metadata").
x86mmx_type("x86mmx").


//------------------------------------------------------------------------------
// [Integer Types] and their selectors
//------------------------------------------------------------------------------

.decl int1_type(type:IntegerType)
.decl int8_type(type:IntegerType)
.decl int32_type(type:IntegerType)
.decl int64_type(type:IntegerType)
.decl boolean_type(type:IntegerType)

integer_type(t) :- int1_type(t).
integer_type(t) :- int8_type(t).
integer_type(t) :- int32_type(t).
integer_type(t) :- int64_type(t).

int1_type("i1").
int8_type("i8").
int32_type("i32").
int64_type("i64").

// Boolean alias
boolean_type(t) :- int1_type(t).

//------------------------------------------------------------------------------
// [Floating Point Types] and their selectors
//------------------------------------------------------------------------------

.decl double_type(type:FPType)
.decl float_type(type:FPType)
.decl half_type(type:FPType)
.decl fp128_type(type:FPType)
.decl x86_fp80_type(type:FPType)
.decl ppc_fp128_type(type:FPType)

fp_type(t) :- double_type(t).
fp_type(t) :- float_type(t).
fp_type(t) :- half_type(t).
fp_type(t) :- fp128_type(t).

double_type("double").
float_type("float").
half_type("half").
fp128_type("fp128").
x86_fp80_type("x86_fp80").
ppc_fp128_type("ppc_fp128").


//------------------------------------------------------------------------------
// [Derived Types]
//
// keywords: derived; first class; aggregate
// http://llvm.org/docs/LangRef.html#array-type
// http://llvm.org/docs/LangRef.html#function-type
// http://llvm.org/docs/LangRef.html#pointer-type
// http://llvm.org/docs/LangRef.html#structure-type
// http://llvm.org/docs/LangRef.html#vector-type
//------------------------------------------------------------------------------

// Type Hierarchy

.type DerivedType = Type
.type FunctionType = DerivedType
.type PointerType = DerivedType
.type VectorType = DerivedType
.type AggregateType = DerivedType
.type ArrayType = AggregateType
.type StructType = AggregateType
.type OpaqueStructType = StructType

.decl derived_type(type:DerivedType)
.decl function_type(type:FunctionType)
.decl pointer_type(type:PointerType)
.decl vector_type(type:VectorType)
.decl aggregate_type(type:AggregateType)
.decl array_type(type:ArrayType)
.decl struct_type(type:StructType)
.decl opaque_struct_type(type:OpaqueStructType)

type(t) :- derived_type(t).
derived_type(t) :- function_type(t).
derived_type(t) :- pointer_type(t).
derived_type(t) :- vector_type(t).
derived_type(t) :- aggregate_type(t).
aggregate_type(t) :- array_type(t).
aggregate_type(t) :- struct_type(t).
struct_type(t) :- opaque_struct_type(t).


//----------------------
// First Class Types
//----------------------

.decl type_is_firstclass(type:Type)

type_is_firstclass(Type) :-
   type(Type),
   !function_type(Type),
   !void_type(Type).


//----------------------
// Array type
//----------------------

.decl array_type_has_component(type:ArrayType, comp:Type)
.decl array_type_has_size(type:ArrayType, size:number)

schema_invalid_type(Type, __FILE__, __LINE__) :-
   schema_sanity(),
   array_type(Type),
   !array_type_has_component(Type, _).

schema_invalid_type(Type, __FILE__, __LINE__) :-
   schema_sanity(),
   array_type(Type),
   !array_type_has_size(Type, _).


.decl int8_array_type(type:ArrayType)

int8_array_type(Type) :-
   array_type_has_component(Type, Comp),
   int8_type(Comp).


//----------------------
// Vector type
//----------------------

.decl vector_type_has_component(type:VectorType, comp:Type)
.decl vector_type_has_size(type:VectorType, size:number)

.decl integer_vector_type(type:VectorType)

integer_vector_type(Type) :-
   vector_type_has_component(Type, Comp),
   integer_type(Comp).
